plugins {
	id 'java'
	id "io.freefair.lombok" version "8.6"
	id "org.springframework.boot" version "3.2.3"
	id 'idea'
	id "org.openapi.generator" version "7.7.0"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: "jacoco"

group = 'com.banking'
version = '1.0.0'

repositories {
	mavenCentral()
}

dependencies {
//    spring
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation('org.springframework.boot:spring-boot-starter-validation')
	implementation('org.springframework.boot:spring-boot-starter-data-jdbc')
	implementation('org.springframework.boot:spring-boot-starter-actuator')

//    json logger
	runtimeOnly('net.logstash.logback:logstash-logback-encoder:7.4')
//    metrics
	implementation('io.micrometer:micrometer-registry-prometheus')

//    migration & JDBC driver
	implementation('org.liquibase:liquibase-core:4.26.0')
	implementation('org.postgresql:postgresql:42.7.3')

//    misc
	implementation('io.vavr:vavr:0.10.4')

//    test
	testImplementation platform('org.junit:junit-bom:5.9.1')
	testImplementation('org.junit.jupiter:junit-jupiter')
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('org.apache.httpcomponents.client5:httpclient5:5.3.1')
	testImplementation 'org.testcontainers:postgresql:1.19.3'
	testImplementation 'io.kubernetes:client-java:21.0.1'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.22'
}

test {
	useJUnitPlatform()
}

tasks.register('copyDependencies', Copy) {
	into project.layout.buildDirectory.dir("dependencies")
	from configurations.compileClasspath + configurations.runtimeClasspath
}

tasks.register('copyTestDependencies', Copy) {
	into project.layout.buildDirectory.dir("testDependencies")
	from configurations.testCompileClasspath + configurations.testRuntimeClasspath
}

jacoco {
	toolVersion = "0.8.11"
}

jacocoTestReport {
	executionData(tasks.withType(Test))
	reports {
		xml.required.set(true)
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					"com/bostongene/cdm/controller/**",
					"com/bostongene/cdm/dto/**",
					"com/bostongene/cdm/model/**",
					"com/bostongene/cdm/KbStorageServiceApplication.class"
			])
		}))
	}
}

test.finalizedBy(jacocoTestReport)
check.finalizedBy(jacocoTestReport, copyDependencies, copyTestDependencies)
